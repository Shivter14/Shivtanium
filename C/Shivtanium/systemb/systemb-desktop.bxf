#import sys.bxf
if "%~1"=="--dialog" (
	shift /1 & shift /1
	goto :dialog.%2
)

set /a "launcherY=sys.modeH-1, launcherW=sys.modeW-(launcherBX=(launcherX=3)+11)-2"
for /l %%# in () do (
	set _expTime=!expTime!
	set expTime=!time:~0,-6!
	if "!_expTime!" neq "!expTime!" (
		echo=¤OV	%\e%[!launcherY!;!launcherX!H%\e%[48;^^!dwm.NIcolor^^!;38;^^!dwm.TTcolor^^!m Shivtanium %\e%[48;^^!dwm.BGcolor^^!;38;^^!dwm.FGcolor^^!m%\e%[!launcherW!X ^^!win[!focusedWindow!]title^^!%\e%[999C%\e%[24D!date! !expTime!
	)
	@sys.pullEvents
	@sys.onEvent click=1
		if not defined focusedWindow (
			if "!mouseYpos!"=="!launcherY!" if !mouseXpos! geq !launcherX! if !mouseXpos! leq !launcherBX! call :launcher
		)
	@sys.onEventRaw "!kernelOut:~0,14!"=="focusedWindow="
		set "!kernelOut!" >nul 2>&1
		if "!focusedWindow!" neq "systemb_launcher" echo=¤OV	%\e%[!launcherY!;!launcherX!H%\e%[48;^^!dwm.NIcolor^^!;38;^^!dwm.TTcolor^^!m Shivtanium %\e%[48;^^!dwm.BGcolor^^!;38;^^!dwm.FGcolor^^!m%\e%[!launcherW!X ^^!win[!focusedWindow!]title^^!%\e%[999C%\e%[24D!date! !expTime!
	@sys.handleStdEvents
)

:launcher
setlocal
echo=¤OV	%\e%[!launcherY!;!launcherX!H%\e%[48;^^!dwm.TIcolor^^!;38;^^!dwm.TTcolor^^!m Shivtanium %\e%[48;^^!dwm.BGcolor^^!;38;^^!dwm.FGcolor^^!m%\e%[!launcherW!X%\e%[999C%\e%[24D!date! !expTime!
set /a "win[systemb_launcher]X=launcherX, tempY=launcherY-1, win[systemb_launcher]W=38, win[systemb_launcher]H=11, win[systemb_launcher]Y=launcherY-win[systemb_launcher]H, tempZ=win[systemb_launcher]Y+1"
if "!sys.lowPerformanceMode!"=="True" (
	set "tempY=!win[systemb_launcher]Y!"
	set theme=classic
	set sys.reduceMotion=True
) else set theme=
echo=¤CW	systemb_launcher	!win[systemb_launcher]X!	!tempY!	!win[systemb_launcher]W!	!win[systemb_launcher]H!	!sys.username!	!theme! noCBUI noWinAero
>>"!sst.dir!\temp\kernelPipe" echo=registerWindow	!PID!	systemb_launcher	!win[systemb_launcher]X!	!tempZ!	!win[systemb_launcher]W!	!win[systemb_launcher]H!	1

set "pipe=	pt=	l1=%\e%[38;^!win[systemb_launcher]TIcolor^!m▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀"
if "!sys.reduceMotion!" neq "True" for /l %%y in (!tempY! -1 !win[systemb_launcher]Y!) do (
	echo=¤MW	systemb_launcher	Y=%%y!pipe!
	set pipe=
)
echo=¤MW	systemb_launcher	Y=!win[systemb_launcher]Y!!pipe!
set pipe=

set "math=programCount=0, win[systemb_launcher]BY=win[systemb_launcher]Y+win[systemb_launcher]H-1, sidebarW=12, itemW=(sidebarX=win[systemb_launcher]W-sidebarW-2)-2"
set "buttons= "

for %%b in (
	"about		sidebarW	sidebarX	win[systemb_launcher]H-9	:about		About"
	"settings	sidebarW	sidebarX	win[systemb_launcher]H-8	:settings	Settings"
	"terminal	sidebarW	sidebarX	win[systemb_launcher]H-7	:terminal	Terminal"
	"explorer	sidebarW	sidebarX	win[systemb_launcher]H-6	:explorer	Explorer"
	"logout		sidebarW	sidebarX	win[systemb_launcher]H-4	:logout		Log Out"
	"restart	sidebarW	sidebarX	win[systemb_launcher]H-3	:restart	Restart"
	"shutdown	sidebarW	sidebarX	win[systemb_launcher]H-2	:shutdown	Shut Down"
	
	"calculator	win[systemb_launcher]W-sidebarW-6	2	2	:calculator	Calculator"
	"taskmgr	win[systemb_launcher]W-sidebarW-6	2	3	:taskmgr	Task Manager"
) do for /f "tokens=1-5* delims=	" %%0 in ("%%~b") do (
	set buttons="%%~0" !buttons!
	set "button[%%~0]=%%~4"
	set "button[%%~0]title= %%~5"
	set "math=!math!, button[%%~0]W=%%~1, button[%%~0]X=%%~2, button[%%~0]Y=%%~3, button[%%~0]BX=button[%%~0]X+button[%%~0]W-1"
)

set /a !math!
set "math=pnw=win[systemb_launcher]W-sidebarW-6"
for %%b in (!buttons!) do for /f "tokens=1* delims=;" %%y in ("!button[%%~b]Y!;!button[%%~b]W!") do (
	set "o%%y=!o%%y!%\e%8%\e%[!button[%%~b]X!C%\e%[!button[%%~b]W!X!button[%%~b]title:~0,%%~z!"
)

set pipe=¤MW	systemb_launcher
for /l %%y in (!win[systemb_launcher]H! -1 1) do (
	if defined o%%y set "pipe=!pipe!	o%%y=!o%%y:~2!"
)
echo=!pipe!
set pipe=¤MW	systemb_launcher
for /d %%A in ("!sst.dir!\programs\*") do if exist "%%~fA\shivtanium.dat" (
	set /a programCount+=1
	for /f "usebackq tokens=1* delims==^!*" %%a in ("%%~fA\shivtanium.dat") do (
		set "program[!programCount!]%%a=%%b"
	)
	set "program[!programCount!]=%%~nxA"
)
set /a "visiblePrograms=win[systemb_launcher]H-4"
if !visiblePrograms! gtr !programCount! set visiblePrograms=!programCount!
for /l %%a in (1 1 !visiblePrograms!) do (
	set buttons=!buttons! %%a
	set "button[%%a]=:launchProgram %%a"
	set "button[%%a]title= !program[%%a]name!"
	set "math=!math!, button[%%a]W=pnw, button[%%a]X=2, button[%%a]Y=%%a+3, button[%%a]BX=button[%%a]X+button[%%a]W-1"
)
set /a !math!
for %%b in (!buttons!) do for /f "tokens=1* delims=;" %%y in ("!button[%%~b]Y!;!button[%%~b]W!") do (
	set "o%%y=!o%%y!%\e%8%\e%[!button[%%~b]X!C%\e%[!button[%%~b]W!X!button[%%~b]title:~0,%%~z!"
)
for /l %%y in (!win[systemb_launcher]H! -1 1) do (
	if defined o%%y set "pipe=!pipe!	o%%y=!o%%y:~2!"
)
echo=!pipe!
set pipe=
set exit=
:launcher.loop
for /l %%# in (1 1 1000) do if not defined exit for /l %%# in (1 1 1000) do if not defined exit (
	set _expTime=!expTime!
	set expTime=!time:~0,-6!
	if "!_expTime!" neq "!expTime!" (
		echo=¤OV	%\e%[!launcherY!;!launcherX!H%\e%[48;^^!dwm.TIcolor^^!;38;^^!dwm.TTcolor^^!m Shivtanium %\e%[48;^^!dwm.BGcolor^^!;38;^^!dwm.FGcolor^^!m%\e%[!launcherW!X%\e%[999C%\e%[24D!date! !expTime!
	)
	@sys.pullEvents
	@sys.onEvent click=1
		if "!focusedWindow!"=="systemb_launcher" (
			set /a "relativeMouseX=mouseXpos-win[systemb_launcher]X, relativeMouseY=mouseYpos-win[systemb_launcher]Y"
			for %%b in (!buttons!) do (
				if "!relativeMouseY!"=="!button[%%~b]Y!" if !relativeMouseX! geq !button[%%~b]X! if !relativeMouseX! leq !button[%%~b]BX! (
					call !button[%%~b]!
					set exit=True
				)
			)
		) else (
			call :exit
			set exit=True
		)
	@sys.handleStdEvents
)
if not defined exit goto launcher.loop
endlocal
exit /b 0
:shutdown
(
	echo=exitProcess	!PID!
	echo=powerState	shutdown
) >> "!sst.dir!\temp\kernelPipe"
exit 0
:restart
(
	echo=exitProcess	!PID!
	echo=powerState	reboot
) >> "!sst.dir!\temp\kernelPipe"
exit 0
:logout
(
	echo=createProcess	0	systemb-login.bat
	echo=exitProcessTree	!sys.UPID!
	echo=exitProcess	!PID!
) >> "!sst.dir!\temp\kernelPipe"
exit 0
:calculator
@sys.call createProcess	!sys.UPID!	systemb-calculator.bat
goto exit
:explorer
@sys.call createProcess	!sys.UPID!	systemb-explorer.bat --username "!sys.username!" --UPID !sys.UPID!
goto exit
:terminal
@sys.call createProcess	!sys.UPID!	systemb-console.bat --username "!sys.username!" --UPID !sys.UPID!
goto exit
:settings
@sys.call createProcess	!sys.UPID!	systemb-control-panel.bat --username "!sys.username!" --UPID !sys.UPID!
goto exit
:taskmgr
@sys.call createProcess	!sys.UPID!	systemb-taskmgr.bat --username "!sys.username!" --UPID !sys.UPID!
goto exit
:about
@sys.call createProcess	!sys.UPID!	systemb-desktop.bat --dialog about !sys.username!
goto exit
:launchProgram
if "!program[%~1]path:~-4!"==".bxf" (
	@sys.call createProcess	!sys.UPID!	systemb-desktop.bat --dialog compile-run "programs\!program[%~1]!\!program[%~1]path!" "!sys.username!" !sys.UPID!
)

@sys.call createProcess	!sys.UPID!	"programs\!program[%~1]!\!program[%~1]path!" --username "!sys.username!" --UPID !sys.UPID!
:exit
if "!sys.reduceMotion!" neq "True" (
	for /l %%y in (!win[systemb_launcher]Y! 1 !sys.modeH!) do echo=¤MW	systemb_launcher	Y=%%y
	echo=¤MW	systemb_launcher	Y=!sys.modeH!
)
echo=¤DW	systemb_launcher
@sys.call unRegisterWindow	systemb_launcher
echo=¤OV	%\e%[!launcherY!;!launcherX!H%\e%[48;^^!dwm.NIcolor^^!;38;^^!dwm.TTcolor^^!m Shivtanium %\e%[48;^^!dwm.BGcolor^^!;38;^^!dwm.FGcolor^^!m%\e%[!launcherW!X !focusedWindow!%\e%[999C%\e%[24D!date! !expTime!
set exit=True
exit /b
:dialog.about
set "sys.username=%~1."
set "sys.hostOS="
for /f "delims=" %%a in ('ver') do set "sys.hostOS=!sys.hostOS!%%a "
set sys.hostOS=!sys.hostOS!
set "secondary_window_params=l9=  Shivtanium version !sys.tag! !sys.ver! !sys.subvinfo!	l10=  Copyright © Shivter 2021 - 2024	l12=  Host OS: !sys.hostOS:~0,47!	l13=  Logged in as !sys.username:~0,43!"
set "extended_window_params=l1=%\e%[48;5;15;38;5;16m▓▓▓▒▒░░%\e%[45X%\e%[45C░░▒▒▓▓	l2=%\e%[48;5;15;38;5;16m▓▓▒▒░░   ▄▄▄ ▄    ▄      ▄%\e%[12X%\e%[12C▄%\e%[12X%\e%[12C░░▒▒▓▓▓	l3=%\e%[48;5;15;38;5;16m▓▓▓▒▒░░ █    █          ▄█▄%\e%[25X%\e%[25C░░▒▒▓▓	l4=%\e%[48;5;15;38;5;16m▓▓▒▒░░   ▀▀▄ █▀▀▄ █ █ █  █  ▄▀▀▄ █▀▀▄ █ █  █ █▀▄▀▄ ░░▒▒▓▓▓	l5=%\e%[48;5;15;38;5;16m▓▓▓▒▒░░ ▄▄▄▀ █  █ █ ▀█▀  █  ▀▄▄█ █  █ █ ▀▄▄█ █ █ █  ░░▒▒▓▓	l6=%\e%[48;5;15;38;5;16m▓▓▒▒░░%\e%[45X%\e%[10C▀%\e%[9C▀%\e%[9C▀%\e%[8C▀     ░░▒▒▓▓▓	l7=%\e%[48;5;15;38;5;16m▓▓▓▒▒░░%\e%[45X%\e%[45C░░▒▒▓▓"
call systemb-dialog.bat (sys.modeW-w)/2+1 3 60 16 "About Shivtanium" "" w-buttonW-2 h-2 7 " Close "
exit /b
:dialog.compile-run
#import str.bxf
set "name=%~n1"
@str.getLen "str=    !name!    " "return=win[!PID!.bxf]W"
if !win[%PID%.bxf]W! lss 24 set "win[!PID!.bxf]W=24"
set /a "textW=win[!PID!.bxf]W-4"

echo=¤CW	!PID!.bxf	5	3	!win[%PID%.bxf]W!	7	BXF Compiler
echo=¤MW	!PID!.bxf	l2=  BXF is setting up:	l3=  %\e%[7m%\e%[!textW!X !name!%\e%[27m
@sys.call registerWindow	!PID!	!PID!.bxf	5	3	!win[%PID%.bxf]W!	6

pushd "%~dp1"
set "ext=>&3 echo(^!input^!"
call "!sst.dir!\core\bxf.bat" "%~nx1" | call "!sst.dir!\core\streamIntoWindow"
if errorlevel 1 (
	@sys.exit
)
@sys.call unRegisterWindow	!PID!.bxf
endlocal
call "%~n1.bat" --username "%~2" --UPID %3
