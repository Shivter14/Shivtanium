#import sys.bxf, dwm.bxf
#function loadPage
	if defined unsavedChanges call :page[!unsavedChanges!].cancel
	@dwm.clearBuffers "from=1" "to=!inbH!" "o=%\e%[!sidebarW!X" "l="
	for /l %%y in (2 1 !pagesBY!) do set "o%%y=!o%%y!!page[%%y]!"
	set "pipe=¤MW	!PID!.control"
	for /l %%y in (1 1 !inbH!) do set "pipe=!pipe!	o%%y=!o%%y!	l%%y="
	@dwm.flushPipe
	if !currentPage! leq !contentH! for %%p in (!currentPage!) do (
		if /I "!sys.reduceMotion!" neq "True" for /l %%w in (1 2 !sidebarW!) do echo=¤MW	!PID!.control	o!currentPage!=!o%%p!%\e%8%\e%[7m!page[%%p]:~0,%%w!%\e%[27m
		set "o!currentPage!=%\e%[!sidebarW!X%\e%[7m!page[%%p]!%\e%[27m"
		echo=¤MW	!PID!.control	o!currentPage!=!o%%p!
	)
	call :page[!currentPage!]
#end
#function makeSelectors
	for /f "tokens=1-4 delims=	" %%0 in (%%b) do (
		set buttons=!buttons! %%0
		set btn[%%0]=:spg %%0 %%1
		set /a "btn[%%0]Y=%%2, btn[%%0]BX=(btn[%%0]X=sidebarW+$x)+2"
		if "!%%1!"=="True" (
			set "btn[%%0]title= ✓ "
		) else set "btn[%%0]title= × "
		set "l%%2=%\e%[!sidebarW!C %%3"
		set "o%%2=!o%%2!%\e%8%\e%[!btn[%%0]X!C!btn[%%0]title!"
	)
#end
(set \n=^^^
%= Newline definition. Do not modify. =%
)
cd "!sst.dir!" || exit /b
for %%a in (%*) do (
	if defined next (
		set "!next!=%%~a"
		set next=
	) else (
		set next=
		if "%%a"=="--username" (
			set next=sys.username
		) else if "%%a"=="--UPID" (
			set next=sys.UPID
		)
	)
)
if not defined sys.username call systemb-dialog.bat (sys.modeW-w)/2 (sys.modeH-h)/2 48 6 "Control Panel" "l2=  Something went wrong:	l3=    Missing parameter: --username" w-buttonW-2 h-2 7 " Close "
if not defined sys.UPID     call systemb-dialog.bat (sys.modeW-w)/2 (sys.modeH-h)/2 48 6 "Control Panel" "l2=  Something went wrong:	l3=    Missing parameter: --UPID"     w-buttonW-2 h-2 7 " Close "
set /a PID=PID
set /a "pagesBY=6, currentPage=2, contentWp=(contentW=(win[!PID!.control]W=64)-(sidebarW=14)-4)-1, contentH=(inbH=(win[!PID!.control]H=16)-1)-1, win[!PID!.control]X=(sys.modeW-win[!PID!.control]W)/2+1, win[!PID!.control]Y=(sys.modeH-win[!PID!.control]H)/2"
echo=¤CW	!PID!.control	!win[%PID%.control]X!	!win[%PID%.control]Y!	!win[%PID%.control]W!	!win[%PID%.control]H!	¤ Control Panel
@sys.call registerWindow	!PID!	!PID!.control	!win[%PID%.control]X!	!win[%PID%.control]Y!	!win[%PID%.control]W!	!win[%PID%.control]H!

set user.globalTheme=aero
for /f "usebackq tokens=1* delims==" %%a in ("!sst.root!\Users\!sys.username!\userprofile.dat") do (
	set "user.%%a=%%b" > nul 2>&1
)
@dwm.getThemeCBUIoffset "theme=!user.globalTheme!" "return=CBUIoffset"
set /a "closeButtonX=win[!PID!.control]W-3+CBUIoffset"

set "page[2]= Performance  "
set "page[3]= Appearance   "
set "page[4]= System       "
set "page[5]= Updates      "
set "page[6]= Keyboard     "
if /I "!sys.lowPerformanceMode!"=="True" set "sys.reduceMotion=True"

set tempstr=
if "!sys.reduceMotion!"=="True" (
	set "pipe=¤MW	!PID!.control"
	for /l %%y in (1 1 !inbH!) do (
		set "pipe=!pipe!	o%%y=!o%%y!"
	)
	echo=!pipe!
) else for /l %%w in (2 2 !sidebarW!) do (
	set "sidebarW=%%w"
	set "pipe=¤MW	!PID!.control"
	for /l %%y in (1 1 !inbH!) do (
		if defined page[%%y] set "tempstr=!page[%%y]:~-%%w!"
		set "pipe=!pipe!	o%%y=%\e%[%%wX!tempstr!"
		set tempstr=
	)
	@dwm.flushPipe
)
@loadPage

for /l %%# in () do (
	set _expTime=!expTime!
	set expTime=!time:~0,-3!
	if "!_expTime!" neq "!expTime!" (
		if defined page[!currentPage!]r call :page[!currentPage!]r
	)
	@sys.pullEvents
	@sys.onEventRaw "!kernelOut!;!focusedWindow!"=="click=1;!PID!.control"
		set /a "relativeMouseX=mouseXpos-win[!PID!.control]X, relativeMouseY=mouseYpos-win[!PID!.control]Y"
		if "!relativeMouseY!"=="0" (
			if !relativeMouseX! geq !closeButtonX! (
				if defined unsavedChanges call :page[!unsavedChanges!].cancel
				@sys.exit
			)
		) else if !relativeMouseX! lss !sidebarW! (
			if defined page[!relativeMouseY!] (
				set currentPage=!relativeMouseY!
				@loadpage
			)
		) else for %%b in (!buttons!) do if "!relativeMouseY!"=="!btn[%%~b]Y!" if !relativeMouseX! geq !btn[%%~b]X! if !relativeMouseX! leq !btn[%%~b]BX! for %%y in (!btn[%%~b]Y!) do echo=¤MW	!PID!.control	o%%y=!o%%y!%\e%8%\e%[!btn[%%~b]X!C%\e%[7m!btn[%%~b]title!%\e%[27m
	@sys.onEventRaw "!kernelOut!;!focusedWindow!"=="click=0;!PID!.control"
		set /a "relativeMouseX=mouseXpos-win[!PID!.control]X, relativeMouseY=mouseYpos-win[!PID!.control]Y"
		for %%b in (!buttons!) do if "!relativeMouseY!"=="!btn[%%~b]Y!" if !relativeMouseX! geq !btn[%%~b]X! if !relativeMouseX! leq !btn[%%~b]BX! call !btn[%%~b]!
	@sys.handleStdEvents
)
:spg
set unsavedChanges=!currentPage!
set "btn[Cancel]title= Cancel "
set "btn[Cancel]=:page[!currentPage!].cancel"
if "!currentPage!"=="3" (
	set "buttons=Cancel Save !buttons:Cancel Save =!"
	set "btn[Save]title= Save "
	set "btn[Save]=:page[!currentPage!].save"
	set /a "btn[Save]X=win[!PID!.control]W-8, btn[Save]Y=contentH, btn[Save]BX=btn[Save]X+7, btn[Cancel]X=btn[Save]X-10, btn[Cancel]Y=contentH, btn[Cancel]BX=btn[Cancel]X+8"
	set "o!contentH!=!o%contentH%!%\e%8%\e%[!btn[Cancel]X!C!btn[Cancel]title!%\e%[2C!btn[Save]title!"
) else (
	set "buttons=Cancel Apply !buttons:Cancel Apply =!"
	set "btn[Apply]title= Apply "
	set "btn[Apply]=:page[!currentPage!].apply"
	set /a "btn[Apply]X=win[!PID!.control]W-9, btn[Apply]Y=contentH, btn[Apply]BX=btn[Apply]X+7, btn[Cancel]X=btn[Apply]X-10, btn[Cancel]Y=contentH, btn[Cancel]BX=btn[Cancel]X+8"
	set "o!contentH!=!o%contentH%!%\e%8%\e%[!btn[Cancel]X!C!btn[Cancel]title!%\e%[2C!btn[Apply]title!"
)

if /I "!%~2!"=="True" (
	set "%~2=False"
	set "btn[%~1]title= × "
) else (
	set "%~2=True"
	set "btn[%~1]title= ✓ "
)
for %%y in (!btn[%~1]Y!) do (
	set "o%%y=%\e%[!sidebarW!X!page[%%y]!"
	if "!currentPage!"=="%%y" set "o%%y=%\e%[!sidebarW!X%\e%[7m!page[%%y]!%\e%[27m"
	set "o%%y=!o%%y!%\e%8%\e%[!btn[%~1]X!C!btn[%~1]title!"
)
set "pipe=¤MW	!PID!.control"
for /l %%y in (1 1 !inbH!) do set "pipe=!pipe!	o%%y=!o%%y!"
@dwm.flushPipe
exit /b 0
:page[3].PickTheme

set lsux.list=theme
set /a "lsux.itemCount=themeCount, win[!PID!.lsux]X=win[!PID!.control]X+sidebarW+2, win[!PID!.lsux]W=contentWp, win[!PID!.lsux]Y=win[!PID!.control]Y+5"
set "theme[!selectedTheme!]o=o"
cmd /c listSelectionUX.bat
set "theme[!selectedTheme!]o="
if errorlevel 1 (
	set unsavedChanges=!currentPage!
	set "buttons=Cancel Save !buttons:Cancel Save =!"
	set "btn[Cancel]title= Cancel "
	set "btn[Cancel]=:page[!currentPage!].cancel"
	set "btn[Save]title= Save "
	set "btn[Save]=:page[!currentPage!].save"
	set /a "btn[Save]X=win[!PID!.control]W-8, btn[Save]Y=contentH, btn[Save]BX=btn[Save]X+7, btn[Cancel]X=btn[Save]X-10, btn[Cancel]Y=contentH, btn[Cancel]BX=btn[Cancel]X+8"
	set "o!contentH!=!o%contentH%!%\e%8%\e%[!btn[Cancel]X!C!btn[Cancel]title!%\e%[2C!btn[Save]title!"
	set selectedTheme=!errorlevel!
	for /f "delims=^!*" %%a in ("!selectedTheme!") do (
		@dwm.getThemeCBUIoffset "theme=!theme[%%a]!" "return=CBUIoffset"
		set /a "closeButtonX=win[!PID!.control]W-3+CBUIoffset"
		echo=¤CTRL	APPLYTHEME	!theme[%%a]!
	)
	echo=¤DW	!PID!.control
	echo=¤CW	!PID!.control	!win[%PID%.control]X!	!win[%PID%.control]Y!	!win[%PID%.control]W!	!win[%PID%.control]H!	Control Panel
) else if "!errorlevel!"=="-1" (
	call :page[!unsavedChanges!].cancel
	exit 0
)
goto page[3].qrld
:page[3] Appearance
for /f "tokens=1 delims==" %%a in ('set btn[ 2^>nul ^& set theme 2^>nul') do set "%%a="

set user.globalTheme=aero
for /f "usebackq tokens=1* delims==" %%a in ("!sst.root!\Users\!sys.username!\userprofile.dat") do (
	set "user.%%a=%%b" > nul 2>&1
)
set buttons=PickTheme CustomThemes
set btn[PickTheme]Y=5
set btn[CustomThemes]Y=11
set "btn[CustomThemes]title= Custom Themes "
set /a "btn[CustomThemes]BX=(btn[CustomThemes]X=btn[PickTheme]X=sidebarW+2)+14, btn[PickTheme]BX=win[!PID!.control]W-4"
set btn[PickTheme]=:page[3].PickTheme
set btn[CustomThemes]=:page[3].CustomThemes

for /f "delims=" %%T in ('dir /b /a:-D "!sst.dir!\temp\themes"') do if /I "%%~xT" neq ".themeMod" (
	set /a themeCount+=1
	set "theme[!themeCount!]=%%~nxT"
	if "!user.globalTheme!"=="%%~nxT" set selectedTheme=!themeCount!
)
if not defined selectedTheme (
	echo=¤CTRL	APPLYTHEME	!theme[1]!
	set selectedTheme=1
)
for /f "delims=" %%a in ('call "!sst.dir!\core\config.bat" --get useAltWinCtrlChars') do set "sys.%%a">nul 2>&1

@dwm.clearBuffers "from=1" "to=!inbH!" "o=%\e%[!sidebarW!X" "l="
for /l %%y in (2 1 !pagesBY!) do set "o%%y=!o%%y!!page[%%y]!"
set "o!currentPage!=%\e%[!sidebarW!X%\e%[7m!page[%currentPage%]!%\e%[27m"
set "l2=%\e%[!sidebarW!C Appearance"
set "l4=%\e%[!sidebarW!C User theme:"
set "l8=%\e%[!sidebarW!C ( This fixes issues on non-UTF-8 fonts,"
set "l9=%\e%[!sidebarW!C   restart required.%\e%[21C)"
set "o11=!o11!%\e%[!btn[CustomThemes]X!C!btn[CustomThemes]title!"
:page[3].qrld
set "l5=%\e%[!sidebarW!C %\e%[7m%\e%[!contentWp!X !theme[%selectedTheme%]:~0,%contentW%!%\e%[27m"

for %%b in (
	"useAltWinCtrlChars	sys.useAltWinCtrlChars	7	Alternatative Window Control Characters"
) do (
	@makeSelectors "x=42"
)

set "pipe=¤MW	!PID!.control"
@dwm.preparePipe "targetWin=!PID!.control" "from=1" "to=!inbH!"
@dwm.flushPipe
exit /b 0
:page[3].CustomThemes
set currentPage=C
@loadPage
exit /b 0
:page[2] Performance
for /l %%y in (1 1 !inbH!) do (
	set "o%%y=%\e%[!sidebarW!X"
	set "l%%y="
)
for /l %%y in (2 1 !pagesBY!) do set "o%%y=!o%%y!!page[%%y]!"
set "o!currentPage!=%\e%[!sidebarW!X%\e%[7m!page[%currentPage%]!%\e%[27m"
for /f "tokens=1 delims==" %%a in ('set btn[ 2^>nul') do set "%%a="

set "l2=%\e%[!sidebarW!C Performance settings"

set buttons=
for %%b in (
	"lowPerformanceMode	sys.lowPerformanceMode	4	Low Performance Mode"
	"reduceMotion	sys.reduceMotion	5	Reduce Motion"
) do (
	@makeSelectors "x=24"
)

set "pipe=¤MW	!PID!.control"
for /l %%y in (1 1 !inbH!) do set "pipe=!pipe!	l%%y=!l%%y!	o%%y=!o%%y!"
@dwm.flushPipe
exit /b 0
:page[2].cancel
for /f "delims=" %%a in ('call "!sst.dir!\core\config.bat" --get lowPerformanceMode --get reduceMotion') do set "sys.%%a">nul 2>&1
set unsavedChanges=
goto page[2]
:page[2].apply
@dwm.clearBuffers "from=1" "to=!inbH!" "o=%\e%[!sidebarW!X" "l="
for /l %%y in (2 1 !pagesBY!) do set "o%%y=!o%%y!!page[%%y]!"
set "o!currentPage!=%\e%[!sidebarW!X%\e%[7m!page[%currentPage%]!%\e%[27m"
call "!sst.dir!\core\config.bat" --set lowPerformanceMode !sys.lowPerformanceMode! --set reduceMotion !sys.reduceMotion!
if "!sys.lowPerformanceMode!"=="True" echo=¤CTRL	APPLYTHEME	classic
(
	echo=config	lowPerformanceMode=!sys.lowPerformanceMode!
	echo=config	reduceMotion=!sys.reduceMotion!
) >> "!sst.dir!\temp\kernelPipe"
>>"!sst.dir!\temp\kernelPipe" echo=createProcess	!PID!	systemb-dialog (sys.modeW-w)/2 (sys.modeH-h)/2 50 8 "Reboot recommended" "l2=  In order to apply your changes to the system	l3=  settings, It's recommended to log out from	l4=  for them to fully apply."	w-buttonW-2 h-2 12 " Understood "
set unsavedChanges=
goto page[2]
:page[3].cancel
set user.globalTheme=aero
for /f "usebackq tokens=1* delims==" %%a in ("!sst.root!\Users\!sys.username!\userprofile.dat") do (
	set "user.%%a=%%b" > nul 2>&1
)
@dwm.getThemeCBUIoffset "theme=!user.globalTheme!" "return=CBUIoffset"
set /a "closeButtonX=win[!PID!.control]W-3+CBUIoffset"
echo=¤CTRL	APPLYTHEME	!user.globalTheme!
echo=¤DW	!PID!.control
echo=¤CW	!PID!.control	!win[%PID%.control]X!	!win[%PID%.control]Y!	!win[%PID%.control]W!	!win[%PID%.control]H!	Control Panel
set unsavedChanges=
goto page[3]
:page[3].save
set "user.globalTheme=!theme[%selectedTheme%]!"
(
	setlocal disableDelayedExpansion
	for /f "tokens=1* delims=." %%a in ('set user.') do (
		echo(%%b
	)
	endlocal
) > "!sst.root!\Users\!sys.username!\userprofile.dat"
call "!sst.dir!\core\config.bat" --set useAltWinCtrlChars !sys.useAltWinCtrlChars!
goto page[3]
:page[4]
@dwm.clearBuffers "from=1" "to=!inbH!" "o=%\e%[!sidebarW!X" "l="
for /l %%y in (2 1 !pagesBY!) do set "o%%y=!o%%y!!page[%%y]!"
set "o!currentPage!=%\e%[!sidebarW!X%\e%[7m!page[%currentPage%]!%\e%[27m"
for /f "tokens=1 delims==" %%a in ('set btn[ 2^>nul') do set "%%a="

set "l2=%\e%[!sidebarW!C System information"
set "l4=%\e%[!sidebarW!C Shivtanium !sys.tag! !sys.ver!"
set "l5=%\e%[!sidebarW!C !sys.subvinfo!"
set "l6=%\e%[!sidebarW!C Desktop environment: %\e%[7m systemb %\e%[27m"
set "l8=%\e%[!sidebarW!C Processor information"
set "l10=%\e%[!sidebarW!C Processor count: %\e%[7m !sys.CPU.count! %\e%[27m"
set "l11=%\e%[!sidebarW!C Processor architecture: %\e%[7m !sys.CPU.architecture:AMD64=x86_64! %\e%[27m"
set "l12=%\e%[!sidebarW!C Processor identifier:"
set "tempstr=!sys.CPU.identifier! "
set "l13=%\e%[!sidebarW!C %\e%[7m !tempstr:~0,%contentW%!%\e%[27m"
set tempstr=

set "pipe=¤MW	!PID!.control"
for /l %%y in (1 1 !inbH!) do (
	echo=¤TW	!PID!.control	l%%y=!l%%y!
	set "pipe=!pipe!	o%%y=!o%%y!"
)
echo=!pipe!
set pipe=
exit /b 0
:page[5].cancel
:page[5] Updates
@dwm.clearBuffers "from=1" "to=!inbH!" "o=%\e%[!sidebarW!X" "l="
for /l %%y in (2 1 !pagesBY!) do set "o%%y=!o%%y!!page[%%y]!"
set "o!currentPage!=%\e%[!sidebarW!X%\e%[7m!page[%currentPage%]!%\e%[27m"
for /f "tokens=1 delims==" %%a in ('set btn[ 2^>nul') do set "%%a="
set "buttons="
set sys.boot.disableUpdateChecks=
if not defined checkedForUpdates (
	set "page[5]r=1"
	if exist "!sst.dir!\temp\pf-updateCheckSvc" del "!sst.dir!\temp\pf-updateCheckSvc" <nul>nul
	start /b cmd /c "!sst.dir!\boot\updateCheckSvc.bat" /q
)
set "l2=%\e%[!sidebarW!C System updates"
set "l4=%\e%[!sidebarW!C Checking for updates. . ."
set "sys.boot.disableUpdateChecks="
for /f "delims=" %%a in ('"!sst.dir!\core\config.bat" --get boot.disableUpdateChecks') do set "sys.%%a" >nul 2>&1
if /I "!sys.boot.disableUpdateChecks!"=="True" (
	set "startupUpdChck=False"
) else set "startupUpdChck=True"
for %%b in (
	"startupUpdChck	startupUpdChck	10	Check for updates on startup"
) do (
	@makeSelectors "x=32"
)

set "pipe=¤MW	!PID!.control"
for /l %%y in (1 1 !inbH!) do set "pipe=!pipe!	l%%y=!l%%y!	o%%y=!o%%y!"
echo=!pipe!
set pipe=
if not defined checkedForUpdates exit /b 0
set checkedForUpdates=
:page[5]r
if not defined checkedForUpdates if exist "!sst.dir!\temp\pf-updateCheckSvc" (
	set checkedForUpdates=True
	if exist "!sst.dir!\temp\update.dat" (
		(
			set /p "update.tag="
			set /p "update.ver="
			set /p "update.subvinfo="
			set /p "update.link="
		) < "!sst.dir!\temp\update.dat"
		set "buttons=!buttons! update"
		set "btn[update]=:page[5]update"
		set "btn[update]title= Update Now "
		set /a "btn[update]BX=(btn[update]X=sidebarW+2)+11, btn[update]Y=8"
		echo=¤TW	!PID!.control	l4=%\e%[!sidebarW!C Update Found^^!
		echo=¤TW	!PID!.control	l5=%\e%[!sidebarW!C Shivtanium !update.tag! !update.ver!
		echo=¤TW	!PID!.control	l6=%\e%[!sidebarW!C !update.subvinfo!
		set "_o8=!o8!"
		set "o8=!o8!%\e%[!btn[update]X!C Update Now "
		echo=¤MW	!PID!.control	o8=!o8!
	) else (
		echo=¤TW	!PID!.control	l4=%\e%[!sidebarW!C You're up to date^^!
		echo=¤MW	!PID!.control	l5=%\e%[!sidebarW!C No updates are avaliable.
	)
)
exit /b 0
:page[5]update
echo=¤MW	!PID!.control	o8=!_o8!
set _o8=
set "@statusReport=echo=¤MW	!PID!.control	l8=%\e%[!sidebarW!C Progress: "
cmd /c "!sst.dir!\boot\update.bat" /r
echo=¤MW	!PID!.control	o8=!o8!
exit /b
:page[5].apply
if "!startupUpdChck!"=="False" (
	call "!sst.dir!\core\config.bat" --set boot.disableUpdateChecks=True
) else call "!sst.dir!\core\config.bat" --set boot.disableUpdateChecks=False
goto page[5]
:page[6]
@dwm.clearBuffers "from=1" "to=!inbH!" "o=%\e%[!sidebarW!X" "l="
for /l %%y in (2 1 !pagesBY!) do set "o%%y=!o%%y!!page[%%y]!"
set "o!currentPage!=%\e%[!sidebarW!X%\e%[7m!page[%currentPage%]!%\e%[27m"
for /f "tokens=1 delims==" %%a in ('set btn[ 2^>nul') do set "%%a="
set buttons=
exit /b
:page[C]
@dwm.clearBuffers "from=1" "to=!inbH!" "o=%\e%[!sidebarW!X" "l="
for /l %%y in (2 1 !pagesBY!) do set "o%%y=!o%%y!!page[%%y]!"
set "o!currentPage!=%\e%[!sidebarW!X%\e%[7m!page[%currentPage%]!%\e%[27m"
for /f "tokens=1 delims==" %%a in ('set btn[ 2^>nul') do set "%%a="
set buttons=create edit import export duplicate delete
set    "btn[create]title=  + Create    "
set      "btn[edit]title=  ¤ Edit      "
set    "btn[import]title= →□ Import    "
set    "btn[export]title= □→ Export    "
set "btn[duplicate]title= □■ Duplicate "
set    "btn[delete]title=  × Delete    "

set "l2=%\e%[!sidebarW!C Custom themes"
set temp.Y=4
for %%a in (!buttons!) do (
	set /a "btn[%%a]BX=(btn[%%a]X=sidebarW+2)+13, btn[%%a]Y=temp.Y, temp.Y+=1"
	for /f "delims=" %%y in ("!btn[%%a]Y!") do set "o%%y=!o%%y!%\e%8%\e%[!btn[%%a]X!C!btn[%%a]title!"
	set "btn[%%a]=:launchApplet %%aTheme"
)
set "l3=%\e%[!sidebarW!C %\e%[38;^!win[!PID!.control]TIcolor^!m▄▄▄▄▄▄▄▄▄▄▄▄▄▄"
set "l!temp.Y!=%\e%[!sidebarW!C %\e%[38;^!win[!PID!.control]TIcolor^!m▀▀▀▀▀▀▀▀▀▀▀▀▀▀"
:page[C]qrld
set "pipe=¤MW	!PID!.control"
for /l %%y in (1 1 !inbH!) do set "pipe=!pipe!	l%%y=!l%%y!	o%%y=!o%%y!"
@dwm.flushPipe
exit /b 0
:launchApplet
@sys.call createProcess	!PID!	systemb\control-panel-applets\%* --UPID !sys.UPID! --username "!sys.username!"
if defined currentPage goto page[!currentPage!]qrld
exit /b 0
