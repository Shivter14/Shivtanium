#import sys.bxf, dwm.bxf, str.bxf
#function reload
	if !win[%PID%.explorer]W! lss 53 (
		for /l %%y in (1 1 !win[%PID%.explorer]H!) do (
			set "o%%y="
			set "l%%y="
		)
		set sidebarW=1 & set sidebarCW=
	) else (
		set /a "sidebarCW=(sidebarW=20)-2"
		for /l %%y in (1 1 !win[%PID%.explorer]H!) do (
			set "o%%y=%\e%[!sidebarW!X"
			set "l%%y="
		)
	)
	if !scrollpos! lss 0 set scrollpos=0
	if !posY! lss 0 set /a scrollpos+=posY
	set addressbar=
	for /f "delims==" %%a in ('set item 2^>nul') do set "%%a="
	set /a "pos=0, spacingW=2, spacingH=1, iconC=(iconW=16)-1, iconVH=(iconH=7)-1, gridW=(ftW=rsW=win[!PID!.explorer]W - sidebarW - 4) / (iconW + spacingW), gfY=(rsH=win[!PID!.explorer]H - 3) / (iconH + spacingH), win[!PID!.explorer]RH=win[!PID!.explorer]H-1, ftW+=2, closeButtonX=win[!PID!.explorer]W-3+CBUIOffset"
	for /f "delims=^!*" %%F in ('dir /b /o:N /a:D 2^>nul ^& dir /b /o:N /a:-D 2^>nul') do (
		set /a "posX=pos %% gridW, posY=(lines=pos / gridW) - scrollpos, pos+=1"
		if !posY! geq 0 if !posY! leq !gfY! (
			set "temp.attrib=%%~aF"
			set "item[!posX!x!posY!]attrib=!temp.attrib!"
			set "item[!posX!x!posY!]=%%~fF"
			set "item[!posX!x!posY!]name=%%~nxF"
			set "item[!posX!x!posY!]icon=[%%~xF]"
			if "%%~xF"=="" set "item[!posX!x!posY!]icon=unknown"
			if "!temp.attrib:~2,1!" neq "a" set "item[!posX!x!posY!]icon="
			
			for %%a in (
				"0 d directory"
				"3 h hidden"
				"4 s system"
			) do for /f "tokens=1-4*" %%v in ("!posX! !posY! %%~a") do (
				if "!temp.attrib:~%%~x,1!"=="%%~y" set "item[%%vx%%w]icon=!item[%%vx%%w]icon!.%%~z"
			)
			
			set /a "item[!posX!x!posY!]X=rsW / gridW * posX + spacingW + sidebarW, item[!posX!x!posY!]Y=posY * (iconH + spacingH) + spacingH + 1, item[!posX!x!posY!]BX=item[!posX!x!posY!]X+iconW-1, item[!posX!x!posY!]BY=item[!posX!x!posY!]Y+iconH-1"
		)
	)
	set "scY=!gfY!"
	if !posY! lss !gfY! set gfY=!posY!

	for /l %%Y in (0 1 !gfY!) do for /l %%X in (0 1 !gridW!) do (
		if not defined icon!item[%%Xx%%Y]icon![1] (
			set found=
			for /f "tokens=1* delims=[]" %%d in ("!item[%%Xx%%Y]icon!") do for %%f in ("!item[%%Xx%%Y]icon!" "[%%d]" "unknown%%~e" "unknown") do if not defined found (
				for %%a in (!sys.activeResourcePacks!) do if exist "!sst.dir!\resourcepacks\%%~a\icons\!iconW!x!iconVH!\%%~f.spr" (
					set "item[%%Xx%%Y]icon=%%~f"
					set found=1
					for /l %%y in (1 1 !iconVH!) do (
						set /p "icon%%~f[%%y]="
					)
				) < "!sst.dir!\resourcepacks\%%~a\icons\!iconW!x!iconVH!\%%~f.spr"
			)
			set found=
		)
		for /f "tokens=1-3 delims=;" %%a in ("!item[%%Xx%%Y]Y!;!item[%%Xx%%Y]BY!;!item[%%Xx%%Y]icon!") do (
			set temp=0
			for /l %%y in (%%a 1 %%b) do (
				set /a temp+=1
				for /f %%z in ("!temp!") do (
					if defined icon%%c[%%z] set "l%%y=!l%%y!%\e%8%\e%[!item[%%Xx%%Y]X!C!icon%%c[%%z]!"
				)
			)
			for /f %%i in ("!iconC!") do set "o%%b=!o%%b!%\e%8%\e%[!item[%%Xx%%Y]X!C%\e%[!iconW!X !item[%%Xx%%Y]name:~0,%%i!"
		)
	)
	if !lines! geq !gfY! (
		set /a "scrollbar.S=scrollpos * rsH / lines + 1, scrollbar.E=(scrollpos + scY - 1) * rsH / lines + 1"
		for /l %%y in (!scrollbar.S! 1 !scrollbar.E!) do (
			set "o%%y=!o%%y!%\e%8%\e%[!win[%PID%.explorer]W!C%\e%[2D "
		)
	)
	if defined sidebarCW (
		set "o3=!o3!%\e%8 %\e%[48;^!win[!PID!.explorer]NIcolor^!m%\e%[!sidebarCW!X Quick access:"
		set "o4=!o4!%\e%8 %\e%[48;^!win[!PID!.explorer]NIcolor^!m%\e%[!sidebarCW!X  Home"
		set "o5=!o5!%\e%8 %\e%[48;^!win[!PID!.explorer]NIcolor^!m%\e%[!sidebarCW!X  sst.dir"
		set "o6=!o6!%\e%8 %\e%[48;^!win[!PID!.explorer]NIcolor^!m%\e%[!sidebarCW!X  sst.root"
		set "o7=!o7!%\e%8 %\e%[48;^!win[!PID!.explorer]NIcolor^!m%\e%[!sidebarCW!X  Temp"
		set "o8=!o8!%\e%8 %\e%[48;^!win[!PID!.explorer]NIcolor^!m%\e%[!sidebarCW!X  Resource Packs"
		set "o9=!o9!%\e%8 %\e%[48;^!win[!PID!.explorer]NIcolor^!m%\e%[!sidebarCW!X  systemb"
	)
	set "dcd= !cd!"
	for /f %%f in ("!ftw!") do set "l1=%\e%[!sidebarW!C%\e%[D!dcd:~-%%f!"
	set "o1=!o1!%\e%8 %\e%[7m ▲ %\e%[27m"
	set "o!win[%PID%.explorer]RH!=%\e%[!win[%PID%.explorer]W!X"
	set "pipe=¤MW	!PID!.explorer"
	@dwm.preparePipe "targetWin=!PID!.explorer" "from=1" "to=!win[%PID%.explorer]RH!"
	@dwm.flushPipe
#end
set /a "win[!PID!.explorer]W=sys.modeW*2/3, win[!PID!.explorer]H=sys.modeH*2/3, win[!PID!.explorer]X=3, win[!PID!.explorer]Y=2"
if !win[%PID%.explorer]W! lss 32 (
	if !sys.modeW! lss 32 (
		set /a "win[!PID!.explorer]W=sys.modeW, win[!PID!.explorer]X=1"
	) else set /a "win[!PID!.explorer]X=(sys.modeW-(win[!PID!.explorer]W=32))/2+1"
)
if !win[%PID%.explorer]H! lss 8 (
	if !sys.modeH! lss 8 (
		set /a "win[!PID!.explorer]H=sys.modeH-1, win[!PID!.explorer]Y=1"
	) else set /a "win[!PID!.explorer]Y=(sys.modeH-(win[!PID!.explorer]H=8))/2"
)
echo=¤CW	!PID!.explorer	!win[%PID%.explorer]X!	!win[%PID%.explorer]Y!	!win[%PID%.explorer]W!	!win[%PID%.explorer]H!	╒═┐ File Explorer
@sys.call registerWindow	!PID!	!PID!.explorer	!win[%PID%.explorer]X!	!win[%PID%.explorer]Y!	!win[%PID%.explorer]W!	!win[%PID%.explorer]H!	001	32	8
set "focusedWindow=!PID!.explorer"

for %%a in (%*) do (
	if defined next (
		set "!next!=%%~a"
		set next=
	) else (
		set next=
		if "%%a"=="--username" (
			set next=sys.username
		) else if "%%a"=="--UPID" (
			set next=sys.UPID
		) else if "%%a"=="--working-dir" (
			set next=setdir
		)
	)
)
if not defined sys.UPID (
%=% call systemb-dialog.bat 5 !tempY! 44 7 "systemb-explorer	classic"^
%=====% "l2=  The process failed to start:	l3=    You need to be logged in,	l4=    in order to use systemb-explorer."^
%=====% 35 5 7 " Close "
)

set user.globalTheme=aero
for /f "usebackq tokens=1* delims==" %%a in ("!sst.root!\Users\!sys.username!\userprofile.dat") do (
	set "user.%%a=%%b" > nul 2>&1
)
@dwm.getThemeCBUIoffset "theme=!user.globalTheme!" "return=CBUIoffset"
@str.getlen "str=!PID!" "return=PIDL"
set /a "_WinInitialsL=PIDL+16"

if defined setdir (
	cd "!setdir!">nul 2>&1
	set setdir=
)

set scrollpos=0
call :reload
for /l %%# in () do (
	@sys.pullEvents
	@sys.onEventRaw "!kernelOut:~0,6!;!focusedWindow!"=="click=;!PID!.explorer"
		set "!kernelOut!">nul 2>&1
		set /a "relativeMouseX=mouseXpos - win[!PID!.explorer]X, relativeMouseY=mouseYpos - win[!PID!.explorer]Y"
		if "!relativeMouseY!"=="0" (
			if "!click!"=="1" (
				if !relativeMouseX! geq !closeButtonX! (
					>>"!sst.dir!\temp\kernelPipe" echo=exitProcess	!PID!
					exit 0
				)
			)
		) else if !relativeMouseX! geq !sidebarW! (
			if "!relativeMouseY!"=="1" (
				set "addressbar= !cd!"
				for /f "" %%w in ("!ftW!") do (
					echo=¤MW	!PID!.explorer	l1=%\e%[!sidebarW!C%\e%[D%\e%[7m!addressbar:~-%%~w!%\e%[27m
				)
			) else if "!relativeMouseY!" neq "!win[%PID%.explorer]RH!" (
				for /l %%Y in (0 1 !gfY!) do for /l %%X in (0 1 !gridW!) do if !relativeMouseX! geq !item[%%Xx%%Y]X! if !relativeMouseX! leq !item[%%Xx%%Y]BX! if !relativeMouseY! geq !item[%%Xx%%Y]Y! if !relativeMouseY! leq !item[%%Xx%%Y]BY! for %%F in ("!item[%%Xx%%Y]!") do (
					set "temp.attr=%%~aF"
					if "!click!"=="1" (
						if "!temp.attr:~0,1!"=="d" (
							set "dir=%%~fF"
							cd "!dir!" > nul 2>&1
							set scrollpos=0
							call :reload
						) else if "%%~xF"==".bat" (
							>>"!sst.dir!\temp\kernelPipe" echo=createProcess	!sys.UPID!	%%~F	--UPID	!sys.UPID!	--username "!sys.username!"
						) else (
							pushd "%%~dpF"
							start %%~nxF
							popd
							call :reload
						)
					)
				)
			)
		) else (
			if "!relativeMouseY!;!click!"=="1;1" if !relativeMouseX! leq 3 if !relativeMouseX! geq 1 (
				cd .. >nul 2>&1
				set scrollpos=0
				call :reload
			)
		)
	@sys.onEventRaw "!kernelOut:~0,14!"=="keysPressedRN="
		set "!kernelOut!" > nul 2>&1
		if "!focusedWindow!"=="!PID!.explorer" if defined addressbar (for %%k in (!keysPressedRN!) do (
			set "char=!charset_L:~%%k,1!"
			if "!keysPressed!" neq "!keysPressed:-16-=!" set "char=!charset_U:~%%k,1!"
			if "!keysPressed!" neq "!keysPressed:-18-=!" set "char=!charset_A:~%%k,1!"
			if "!char!"==" " (
				if "%%~k" neq "32" set char=
				if "%%~k"=="8" (
					if "!addressbar!" neq " " (
						set "addressbar=!addressbar:~0,-1!"
						if "!keysPressed!" neq "!keysPressed:-16-=!" set "addressbar= "
						for /f "" %%w in ("!ftW!") do (
							echo=¤MW	!PID!.explorer	l1=%\e%[!sidebarW!C%\e%[D%\e%[7m!addressbar:~-%%~w!%\e%[27m
						)
					)
				) else if "%%~k"=="13" (
					cd "!addressbar:~1!">nul 2>&1 || (
						for /f "" %%w in ('set /a !ftW!-7') do (
							set /a "Xpos=win[!PID!.explorer]X+sidebarW+2, Ypos=win[!PID!.explorer]Y+2"
							echo=createProcess	!sys.UPID!	systemb-dialog.bat !Xpos! !Ypos! !rsW! 7 "File Explorer: Invalid directory" "l2=  The directory you entered is invalid:	o3=%\e%[3C!addressbar:~-%%~w!" w-buttonW-2 h-2 7 " Close "
							set Xpos=
							set Ypos=
						) >> "!sst.dir!\temp\kernelPipe"
					)
					set scrollpos=0
					call :reload
				) else if "%%~k"=="27" (
					echo=¤MW	!PID!.explorer	l1=!l1!
					set addressbar=
				)
			)
			if defined char (
				set "addressbar=!addressbar!!char!"
				for /f "" %%w in ("!ftW!") do (
					echo=¤MW	!PID!.explorer	l1=%\e%[!sidebarW!C%\e%[D%\e%[7m!addressbar:~-%%~w!%\e%[27m
				)
			)
		)) else if "!keysPressedRN!"=="  38 " (
			set /a scrollpos-=1
			@reload
		) else if "!keysPressedRN!"=="  40 " (
			set /a scrollpos+=1
			@reload
		)
	@sys.onEvent resizingWindow=!PID!.explorer
		@dwm.clearBuffers "from=1" "to=!sys.modeH!" "o=%\e%[!sidebarW!X"
		set $o=
		@dwm.preparePipe "targetWin=!PID!.explorer"
		@dwm.flushPipe
	@sys.onEventRaw "!kernelOut!;!focusedWindow!"=="resizingWindow=;!PID!.explorer"
		@dwm.clearBuffers "from=!win[%PID%.explorer]H!" "to=!sys.modeH!" "o="
		@dwm.preparePipe "targetWin=!PID!.explorer"
		@dwm.flushPipe
		set resizingWindow=
		call :reload
		echo=¤CTRL	DUMP	mainbuffer=^^!dwm.scene^^!	nul
	@sys.handleStdEvents
)

:reload
@reload
exit /b
