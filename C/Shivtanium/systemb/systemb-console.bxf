#import sys.bxf, dwm.bxf
for %%a in (%*) do (
	if defined next (
		set "!next!=%%~a"
		set next=
	) else (
		set next=
		if "%%a"=="--username" (
			set next=sys.username
		) else if "%%a"=="--UPID" (
			set next=sys.UPID
		) else if "%%a"=="--working-dir" (
			set next=setdir
		)
	)
)
set /a "closeButtonX=(conW=(win[!PID!.conhost]W=64)-2)-1, conP=(conH=(win[!PID!.conhost]H=16)-4)+2, win[!PID!.conhost]X=(sys.modeW-win[!PID!.conhost]W)/2, win[!PID!.conhost]Y=(sys.modeH-win[!PID!.conhost]H)/2"
set "conprompt=[!sys.username! ^!cd:!sst.root!=~^!]$"

echo=¤CW	!PID!.conhost	!win[%PID%.conhost]X!	!win[%PID%.conhost]Y!	!win[%PID%.conhost]W!	!win[%PID%.conhost]H!	Terminal	matrix
echo=¤MW	!PID!.conhost	l!conP!=%conprompt%
@sys.call registerWindow	!PID!	!PID!.conhost	!win[%PID%.conhost]X!	!win[%PID%.conhost]Y!	!win[%PID%.conhost]W!	!win[%PID%.conhost]H!	001	32	4
set "focusedWindow=!PID!.conhost"
set "input= "
for /l %%# in () do (
	@sys.pullEvents
	@sys.onEventRaw "!kernelOut:~0,14!"=="keysPressedRN="
		set "!kernelOut!" > nul 2>&1
		if "!focusedWindow!"=="!PID!.conhost" for %%k in (!keysPressedRN!) do (
			set "char=!charset_L:~%%k,1!"
			if "!keysPressed!" neq "!keysPressed:-16-=!" set "char=!charset_U:~%%k,1!"
			if "!keysPressed!" neq "!keysPressed:-17-=!" set "char=!charset_A:~%%k,1!"
			if "!char!"==" " (
				if "%%~k" neq "32" set char=
				if "%%~k"=="8" (
					if "!input!" neq " " set "input=!input:~0,-1!"
					echo=¤MW	!PID!.conhost	l!conP!=%conprompt%!input!
				) else if "%%~k"=="13" (
					if /I "!input!"==" exit" (
						>>"!sst.dir!\temp\kernelPipe" echo=exitProcess	!PID!
						exit 0
					)
					for /l %%y in (1 1 !conH!) do (
						set /a "x=%%y+1"
						for %%z in (!x!) do set "l%%y=!l%%z!"
					)
					set "l!x!=%conprompt%!input!"
					for /f "delims=*^!" %%a in ('!input! 2^>^&1') do (
						for /l %%y in (1 1 !conH!) do (
							set /a "x=%%y+1"
							for %%z in (!x!) do set "l%%y=!l%%z!"
						)
						set "text=%%a"
						set "text=!text:	=    !"
						if "!text!"=="!text:%\e%=!" set "text=!text:~0,%conW%!"
						set "l!x!=!text!"
						set pipe=
						for /l %%y in (1 1 !conP!) do set "pipe=!pipe!	l%%y=!l%%y!"
						echo=¤MW	!PID!.conhost	!pipe!
						set pipe=
					)
					set "input= "
					echo=¤MW	!PID!.conhost	l!conP!=%conprompt%!input!
				) else if "%%~k"=="27" (
					set "input= "
					echo=¤MW	!PID!.conhost	l!conP!=%conprompt%!input!
				)
			)
			if defined char (
				set "input=!input!!char!"
				echo=¤MW	!PID!.conhost	l!conP!=%conprompt%!input!
			)
		)
	@sys.onEvent click=1
		set /a "click=1, relativeMouseX=mouseXpos-win[!PID!.conhost]X, relativeMouseY=mouseYpos-win[!PID!.conhost]Y"
		if "!relativeMouseY!"=="0" if !relativeMouseX! geq !closeButtonX! (
			>>"!sst.dir!\temp\kernelPipe" echo=exitProcess	!PID!
			exit 0
		)
	@sys.onEvent resizingWindow=!PID!.conhost
		@dwm.clearBuffers "from=1" "to=!win[%PID%.conhost]H!"
		@dwm.preparePipe "targetWin=!PID!.conhost"
		@dwm.flushPipe
	@sys.onEventRaw "!kernelOut!!focusedWindow!"=="resizingWindow=!PID!.conhost"
		set resizingWindow=
		set /a "closeButtonX=(conW=(win[!PID!.conhost]W)-2)-1, conP=(conH=(win[!PID!.conhost]H)-4)+2"
		@dwm.clearBuffers "from=1" "to=!win[%PID%.conhost]H!"
		set "l!conP!=%conprompt%!input!"
		@dwm.preparePipe "targetWin=!PID!.conhost"
		@dwm.flushPipe
	@sys.handleStdEvents
)
