#import sys.bxf, str.bxf
(
	set /p "update.tag="
	set /p "update.ver="
	set /p "update.subvinfo="
	set /p "update.link="
) < "!sst.dir!\temp\update.dat"
if "%~1"=="/r" (
	if not defined @statusReport set "@statusReport=%~2"
	set embeddedMode=1
	call :updateNow /r
	exit /b 0
)
set "@statusReport=echo=¤MW	svc.update	l4=  ^> "
set "btn[later]T= Later "
set "btn[update]T= More Info "
set "verString=!update.tag! !update.ver! !update.subvinfo!"
@str.getLen "str=!verString!" "return=verStringLen"
if !verStringLen! lss 15 set verStringLen=15

set /a "win[svc.update]X=sys.modeW-(win[svc.update]W=15+verStringLen)-1, win[svc.update]Y=sys.modeH-(win[svc.update]H=6)-1, tempY=sys.modeH-1, contentW=(topUI=win[svc.update]W-2)-13, btn[later]X=(btn[later]B=(btn[update]X=(btn[update]B=topUI-1)-(btn[update]Z=10))-2)-(btn[later]Z=6), CBY=win[svc.update]H-2"
set quickbuild=pt=	l1=%\e%8%\e%[A%\e%[38;^^!win[svc.update]TIcolor^^!m█^^!dwm.bottombuffer:~0,!topUI!^^!█%\e%8%\e%[C%\e%[38;^^!win[svc.update]FGcolor^^!m A new update is avaliable^^!\x^^!	l2= Version: %\e%[7m !verString:~0,%contentW%! %\e%[27m
echo=¤CW	svc.update	!win[svc.update]X!	!sys.modeH!	!win[svc.update]W!	!win[svc.update]H!	Update Service	noCBUI noUnfocusedColors
echo=¤MW	svc.update	!quickbuild!	o!CBY!=%\e%[!btn[later]X!C!btn[later]T!%\e%[C!btn[update]T!
@sys.call registerWindow	!PID!	svc.update	!win[svc.update]X!	!win[svc.update]Y!	!win[svc.update]W!	!win[svc.update]H!	1
for /l %%a in (!tempY! -1 !win[svc.update]Y!) do echo=¤MW	svc.update	Y=%%a
set tempY=
for /l %%# in () do (
	@sys.pullEvents
	@sys.onEventRaw "!kernelOut!;!focusedWindow!"=="click=1;svc.update"
		set /a "relativeMouseX=mouseXpos-win[svc.update]X, relativeMouseY=mouseYpos-win[svc.update]Y"
		if "!relativeMouseY!"=="!CBY!" if !relativeMouseX! geq !btn[later]X! if !relativeMouseX! leq !btn[update]B! (
			if !relativeMouseX! leq !btn[later]B! (
				echo=¤MW	svc.update	o!CBY!=%\e%[!btn[later]X!C%\e%[7m!btn[later]T!%\e%[27m%\e%[C!btn[update]T!
			) else if !relativeMouseX! geq !btn[update]X! echo=¤MW	svc.update	o!CBY!=%\e%[!btn[later]X!C!btn[later]T!%\e%[C%\e%[7m!btn[update]T!%\e%[27m
		)
	@sys.onEventRaw "!kernelOut!;!focusedWindow!"=="click=0;svc.update"
		set /a "relativeMouseX=mouseXpos-win[svc.update]X, relativeMouseY=mouseYpos-win[svc.update]Y"
		echo=¤MW	svc.update	o!CBY!=%\e%[!btn[later]X!C!btn[later]T!%\e%[C!btn[update]T!
		if "!relativeMouseY!"=="!CBY!" if !relativeMouseX! geq !btn[later]X! if !relativeMouseX! leq !btn[update]B! (
			if !relativeMouseX! leq !btn[later]B! (
				for /l %%a in (!win[svc.update]Y! 1 !sys.modeH!) do echo=¤MW	svc.update	Y=%%a
				@sys.exit
			) else if !relativeMouseX! geq !btn[update]X! (
				if "!moreInfoShown!" neq "True" (
					set /a "temp.animLen=10, temp.tW=win[svc.update]W+6, temp.tH=12, temp.tX=(sys.modeW-temp.tW)/2, temp.tY=(sys.modeH-temp.tH)/2"
					if /I "!sys.reduceMotion!" neq "True" (
						for /l %%a in (!temp.animLen! -1 1) do (
							set /a "x=temp.tX+(win[svc.update]X-temp.tX)*%%a/temp.animLen, y=temp.tY+(win[svc.update]Y-temp.tY)*%%a/temp.animLen"
							echo=¤MW	svc.update	X=!x!	Y=!y!
						)
						echo=¤MW	svc.update	X=!temp.tX!	Y=!temp.tY!
						for /l %%a in (1 1 !temp.animLen!) do (
							set /a "w=win[svc.update]W+(temp.tW-win[svc.update]W)*%%a/temp.animLen, CBZ=CBY, CBY=(h=win[svc.update]H+(temp.tH-win[svc.update]H)*%%a/temp.animLen)-2"
							echo=¤CW	svc.update	!temp.tX!	!temp.tY!	!w!	!h!	Update Service	noCBUI noUnfocusedColors
							echo=¤MW	svc.update	!quickbuild!	o!CBZ!=	o!CBY!=%\e%[!btn[later]X!C!btn[later]T!%\e%[C!btn[update]T!
						)
					) else echo=¤DW	svc.update
					set /a "win[svc.update]X=temp.tX, win[svc.update]Y=temp.tY, win[svc.update]W=temp.tW, win[svc.update]H=temp.tH, contentW=(topUI=win[svc.update]W-2)-13, btn[later]X=(btn[later]B=(btn[update]X=(btn[update]B=topUI-1)-(btn[update]Z=7))-2)-(btn[later]Z=6), CBY=win[svc.update]H-2"
					set "btn[update]T= Update "
					set quickbuild=pt=	l1=%\e%8%\e%[A%\e%[38;^^!win[svc.update]TIcolor^^!m█^^!dwm.bottombuffer:~0,!topUI!^^!█%\e%8%\e%[C%\e%[38;^^!win[svc.update]FGcolor^^!m A new update is avaliable^^!\x^^!	l2= Version: %\e%[7m !verString:~0,%contentW%! %\e%[27m
					echo=¤CW	svc.update	!temp.tX!	!temp.tY!	!temp.tW!	!temp.tH!	Update Service	noCBUI noUnfocusedColors
					echo=¤MW	svc.update	!quickbuild!	o!CBY!=%\e%[!btn[later]X!C!btn[later]T!%\e%[C!btn[update]T!
					@sys.call modifyWindowProperties	svc.update	!temp.tX!	!temp.tY!	!temp.tW!	!temp.tH!
					set x= & set y= & set w= & set h= & set temp.tX= & set temp.tY= & set temp.tW= & set temp.tH=
					set moreInfoShown=True
				) else (
					echo=¤MW	svc.update	o!CBY!=	l!CBY!= Please wait. . .
					call :updateNow
					@sys.exit
				)
			)
		)
	@sys.onEventRaw "!kernelOut:~0,14!;!focusedWindow!"=="keysPressedRN=;svc.update"
		set "!kernelOut!" >nul 2>&1
		if "!keysPressedRN!" neq "!keysPressedRN: 27 =!" (
			@sys.exit
		)
	@sys.handleStdEvents
)
:updateNow
if not exist "!sst.root!" (
	call :errorbox "Update failed" "'sst.root' does not exist."
	exit /b 1
)
set "targetdir=Shivtanium !update.tag! !update.ver! !update.subvinfo!"
set "targetdir=!targetdir: =_!"
set "targetdir=!targetdir:..=!"
set "targetdir=!targetdir:\=_!"
set "targetdir=!targetdir:/=_!"
set "targetdir=!sst.root!\!targetdir!"
if exist "!targetdir!" call :errorbox "Update failed" "The destination already exists:" "~:\Shivtanium !update.tag! !update.ver! !update.subvinfo!" "You might have already updated." "Reboot into the newer installation."

md "!sst.dir!\temp\update" || (
	call :errorbox "Update failed" "Failed to create temp folder:" "temp\update"
)
pushd "!sst.dir!\temp\update" || (
	call :errorbox "Update failed" "Failed to PUSHD into temp folder:" "temp\update"
)
%@statusReport%Downloading

echo=¤CW	!PID!.getAssets	5	3	84	8	Downloading update files	classic noCBUI
@sys.call registerWindow	!PID!	!PID!.getAssets	5	3	84	8
echo=¤MW	!PID!.getAssets	l2=  Downloading %\e%[7m package.zip %\e%[27m . . .	l4=  %% Total    %% Received %% Xferd  Average Speed   Time    Time     Time  Current	l5=                                 Dload  Upload   Total   Spent    Left  Speed
curl -Lo "package.zip" "!update.link!" 2>&1 | call "!sst.dir!\core\streamIntoWindow" !PID!.getAssets	l6=  
if errorlevel 1 (
	call :errorbox "Failed to download assets" "The following file failed to download:" "%\e%[7m package.zip %\e%[27m" "Reason: %\e%[7m curl error !errorlevel! %\e%[27m"
) else if not exist "package.zip" (
	call :errorbox "Failed to download assets" "The following file failed to download:" "%\e%[7m package.zip %\e%[27m" "Reason: Generic error !errorlevel!"
)
%@statusReport%Extracting
echo=¤MW	!PID!.getAssets	l2=  Extracting %\e%[7m package.zip %\e%[27m . . .	l4=	l6=
tar -xf "package.zip" > tar.log 2>&1 || (
	@sys.call createProcess	!PID!	systemb-textview tar.log
	call :errorbox "Failed to extract assets" "Failed to extract the package." "The TAR logs should be opened. If not, you can find them in:" " %\e%[7m ~:\Shivtanium !update.tag! !update.ver! !update.subvinfo!\temp\tar.log %\e%[7m"
)
echo=¤DW	!PID!.getAssets
@sys.call unRegisterWindow	!PID!.getAssets
%@statusReport%Validating
if not exist "C\Shivtanium\main.bat" (
	call :errorbox "Update failed" "Failed to validate the downloaded installation." "File not found:" "%\e%[7m C\Shivtanium\main.bat %\e%[27m"
)
%@statusReport%Moving files
move C\Shivtanium "!targetdir!" < nul > move.log 2>&1 || (
	call :errorbox "Update failed" "Failed to move the downloaded installation;" "Source: %\e%[7m temp\update\C\Shivtanium %\e%[27m" "Destination: %\e%[7m ~:\Shivtanium !update.tag! !update.ver! !update.subvinfo! %\e%[27m"
)
%@statusReport%Finishing
(
	echo=# This file contains information about the installation that installed this
	echo=# installation via the "Install Upgrade" option.
	echo=# ( Wow that's a lot of installation :D )
	echo=tag=!sys.tag!
	echo=ver=!sys.ver!
	echo=subvinfo=!sys.subvinfo!
	echo=dir=!sys.dir!
) > "upgrade.dat"

echo=¤DW	!PID!.getAssets
@sys.call unRegisterWindow	!PID!.getAssets
cd "!sst.dir!" || exit 1

call :errorbox "Update finished" "The first part of the update was finished successfully." "Reboot, and there should be new boot entries."
:errorbox
if exist "!sst.dir!\temp\update" (
	popd
	rd /s /q "!sst.dir!\temp\update" > nul
)
set halt.title=
set halt.winparams=
set halt.winW=16
set lines=1
for %%a in (%*) do (
	set "msg=#%%~a"
	set "msglen=0"
	for /l %%b in (9,-1,0) do (
		set /a "msglen|=1<<%%b"
		for %%c in (!msglen!) do if "!msg:~%%c,1!" equ "" set /a "msglen&=~1<<%%b"
	)
	
	if not defined halt.title (
		set /a msglen+=13
		set "halt.title=%%~a"
	) else (
		set /a "msglen+=6, lines+=1"
		set "halt.winparams=!halt.winparams!	l!lines!=  %%~a"
	)
	if !msglen! gtr !halt.winW! set "halt.winW=!msglen!"
)
set /a "halt.winH=lines + 4"
call systemb-dialog.bat 5 3 !halt.winW! !halt.winH! "!halt.title!	classic noCBUI" "!halt.winparams!" w-buttonW-2 h-2 7 " Close " True
if defined embeddedMode exit 0
@sys.exit 0
